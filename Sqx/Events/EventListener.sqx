call compile preprocessFileLineNumbers "Sqx\Events\Event.sqx";
call compile preprocessFileLineNumbers "Sqx\Events\MpEventArg.sqx";

/*
 * Name:	EventHandler
 * Date:	2017-06-29
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Models event handling for custom events.
 */
namespace Sqx.Events
{
	public class EventListener
	{
		private fields ["_mEvents" as Array];
	
		// Creates an EventHandler object.
		// _name (String): Name of the event listener. Must be unique for the event handler to be
		//                 able to raise the events on all machines in multiplayer.
		public constructor {
			params ["_name" as String];
			
			_self.Name = _name;
			_mEvents = [];
			
			[_self] call _self.RegisterEventListenerForMultiplayer;
		};
		
		// Gets the name of the event handler.
		public property String Name { get; private set; };
		
		// Adds an event to the event listener.
		// _name (String):    The name (go code) of the event.
		// _function (Code):  The function to be executed when event is raised.
		public method RegisterEvent {
			params ["_eventId" as Scalar, "_function" as Code, "_owner" as Any];
			//player sideChat str _owner;
			_mEvents pushBack [_eventId, _function, 0] new Event;
		};
		
		// Raises an event.
		// _eventName (String): The name (go code) of the event.
		// _parameter1 (Any): Optional. A parameter that is sent to the function. Default 0.
		// _broadcast (Boolean): Optional. Whether the event should fire on all machines or not. Default true.
		public method RaiseEvent {
			params ["_eventId" as Any, ["_parameter" as Any, 0], ["_broadcast" as Boolean, true]];
			
			if (_broadcast) then {
				Engima_Events_EventListener_RaiseEventEventArgs = [_self.Name, _eventId, _parameter] new MpEventArg;
				publicVariable "Engima_Events_EventListener_RaiseEventEventArgs";
			};
			
			{
				if (_x.Id == _eventId) then {
					[_x.Owner, _parameter] spawn _x.Function;
				};
			} foreach _mEvents as Event;
		};
		
		// Registers an event listener for multiplayer use.
		private static method RegisterEventListenerForMultiplayer {
			params ["_eventListener" as EventListener];
			
			if (isNil "Engima_Events_EventListener_registeredEventListeners") then {
				Engima_Events_EventListener_registeredEventListeners = [];
			};
			
			Engima_Events_EventListener_registeredEventListeners pushBack _eventListener;
		};
		
		// Raises an event for a particular event listener
		// _eventHandlerName (String): Name of the eventhandler to fire event upon.
		// _eventName (String):        Name of the event to fire.
		public static method RaiseEventStatic {
			params ["_eventHandlerName" as String, "_eventId" as Any, ["_parameter" as Any, 0], ["_broadcast" as Boolean, true]];
			
			{
				if (_x.Name == _eventHandlerName) then {
					[_eventId, _broadcast, _parameter] call _x.RaiseEvent;
				};
			} foreach Engima_Events_EventListener_registeredEventListeners as EventListener;
		};
	};
	
	#region Global Multiplayer Mechanics
	
	// Old fashioned eventhandler that handles the event that are raised on another machine.
	"Engima_Events_EventListener_RaiseEventEventArgs" addPublicVariableEventHandler {
		params ["_eventArgs" as MpEventArg];
		
		[_eventArgs.EventListenerName, _eventArgs.EventName, _eventArgs.Parameter, false] call EventListener.RaiseEventStatic;
		Engima_Events_EventListener_RaiseEventEventArgs = classNull;
	};
	
	#endregion
	
};
