/*
 * Name:	ExtractionZone
 * Date:	2017-07-03
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Models an extraction zone.
 */

namespace Mission.Shared.Types
{
	public class ExtractionZoneHandler
	{
		private fields ["_mZones" as Array];
	
		// Creates an ExtractionZoneHandler object.
		public constructor {
			_mZones = [];
		};
		
		// Adds a zone.
		// _zone (ExtractionZone): The extraction zone to add.
		public method AddZone {
			params ["_zone" as ExtractionZone];
			
			_mZones pushBack _zone;
		};
		
		// Gets a zone.
		// _point (ExtractionPoint): The identifying extraction point.
		// Returns: The matching extraction zone. classNull if the zone does not exist.
		public method ExtractionZone GetZone {
			params ["_point" as ExtractionPoint];
			private ["_zone" as ExtractionZone];
			
			_zone = classNull;
			
			{
				if (_x.Point == _point) then {
					_zone = _x;
				};
			} foreach _mZones as ExtractionZone;
			
			_zone
		};
		
		// Creates helipads on all extraction points.
		public method CreateHelipads {
			{
				"Land_HelipadEmpty_F" createVehicle _x.Position;
			} foreach _mZones as ExtractionZone;
		};
	};
	
	gExtractionZoneHandler = new ExtractionZoneHandler;
};
